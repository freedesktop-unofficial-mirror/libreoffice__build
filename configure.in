AC_INIT(ooo-build, 2.1.17)
AC_PREREQ(2.51)
AC_CONFIG_SRCDIR(bin/build-ooo)
AC_PREFIX_DEFAULT(/usr)

DEFAULT_TAG=OOE680_m6

# The option tar-ustar is necessary because of some patches have too long
# names so that they cannot be archived using the old V7 format
# This option has been supported since automake 1.8b
AM_INIT_AUTOMAKE([1.8b tar-ustar])

OOO_BUILDVERSION=AC_PACKAGE_VERSION
AC_SUBST(OOO_BUILDVERSION)

PROPAGATED_ARGS=$ac_configure_args
AC_SUBST(PROPAGATED_ARGS)

AC_ARG_WITH(tag,
[
  --with-tag              define the tag to use, defaults to the latest
			  release
			  
			  Example:  --with-tag=src680-mXYZ],
,)

AC_ARG_WITH(srcdir,
[
  --with-srcdir           define the directory where to download the source
			  tarball and other extra sources.],
,)

AC_ARG_WITH(win32,
[
  --with-win32            define the build to be on win32; this sets a number
			  of defaults suitable for that platform.],
,)

AC_ARG_WITH(distro,
[
  --with-distro           build with a specific distributions patch-set. There
			  is no default value, so you always have to select one.
			  			  
			  Examples:  --with-distro=Ark
			             --with-distro=Debian
				     --with-distro=SUSE
			  
			  Hint: Run once without this option to get full list
			  of available distributions ;-)],
,)

AC_ARG_WITH(lang,
[
  --with-lang             define the localizations to build. The English one
			  is built by default.

			  Examples:  --with-lang="en-US de fr it"
				     --with-lang=ALL],
,)

AC_ARG_WITH(poor-help-localizations,
[
  --with-poor-help-localizations
			define the localizations with so incomplete help
			that the English(US) one should be used instead.

			Example:  --with-poor-help-localizations="af ar ca"],
,)


AC_ARG_ENABLE(dbgutil,
[  --enable-dbgutil:       Include additional debugging utilities, such as
                          assertions, object counting, etc. Larger build.
                          (this generates a non-product build)
],,)

AC_ARG_ENABLE(binfilter,
[  --enable-binfilter:     Enable legacy binary file formats filters
                          this makes source download size larger
],,)

AC_ARG_ENABLE(odk,
[  --disable-odk           OO.o includes an ODK, office development kit
                          which some packagers may with to build without
],,)

AC_ARG_WITH(mingwin,
[  --with-mingwin          For Windows users, use the mingwin32 compiler within
                          cygwin environment, this implies --with-use-shell=tcsh

                          Usage: --with-mingwin=yes

                           For !Windows use, use the mingw32 C++ compiler to (re-)
                           build unowinreg.dll

                          Usage: --with-mingwin=i586-mingw32msvc-g++
],,)

AC_ARG_ENABLE(mono,
[  --enable-mono           Enables the compilation of the Mono bindings
],,)

AC_ARG_ENABLE(graphite,
[  --enable-graphite       Enables hyper-complex text rendering with
			  graphite enabled fonts.
],,)

AC_ARG_ENABLE(access,
[  --disable-access        Disable the Access import pieces.
],,)

AC_ARG_WITH(system-mdbtools,
[
  --with-system-mdbtools     Use system libmdb* for the Access base backend
],,)

AC_ARG_WITH(system-libwpd,
[
  --with-system-libwpd     Use system libwpd* library
],,)

AC_ARG_WITH(system-libwps,
[
  --with-system-libwps     Use system libwps* library
],,)

AC_ARG_ENABLE(cairo,
[
  --enable-cairo          Enables cairo canvas backend.],
,)

AC_ARG_ENABLE(hunspell,
[
  --enable-hunspell       Enables Hunspell UNO component.],
,)

AC_ARG_WITH(system-hunspell,
[
  --with-system-hunspell  Use system libhunspell to build the Hunspell
			  UNO component],
,)

AC_ARG_WITH(ooo-builddir,
[
  --with-ooo-builddir     define the directory where openoffice.org will be
			  compiled, e.g. the root of an ooo cvs checkout.],
,)

AC_ARG_WITH(installed-ooo-dirname,
[
  --with-installed-ooo-dirname
			  specify the directory name of the core OOo network
			  install dir, for example "openoffice" or "ooo-1.1",
			  yielding a core install directory of
			  /usr/lib/openoffice or /usr/lib/ooo-1.1.

			  Example:  --with-installed-ooo-dirname=ooo-1.1],
,)

AC_ARG_WITH(binsuffix,
[
  --with-binsuffix
			  specify the suffix to be used on the end of script
			  names, to allow parallel installation of two versions.
			  
			  Default:  --with-binsuffix=2.0],
,)

AC_ARG_WITH(docdir,
[
  --with-docdir           define the directory name where the extra
			  documentation will be installed.

			  Example:  --with-docdir=/usr/share/doc/packages/OpenOffice.org],
,)

AC_ARG_WITH(num-cpus,
[
  --with-num-cpus         Number of build processes/cpus to use (number of
			  projects that will build at the same time).
			  Multi-process/multi-cpu builds can save a lot of time
			  on multi-cpu machines.],
,)

AC_ARG_WITH(gcc-speedup,
[
  --with-gcc-speedup      Tool to use to speedup the compilation. Currently
			  ccache (caching results of previous compilation) and
			  icecream (distributed compiling tool) are supported.
			  Both can be used at the same time.

			  Example: --with-gcc-speedup=ccache,icecream],
,)

AC_ARG_WITH(icecream-max-jobs,
[
  --with-icecream-max-jobs
			  Maximum number of jobs that make will issue at
			  the same time. Defaults to 10.],
,)

AC_ARG_WITH(icecream-bindir,
[
  --with-icecream-bindir  Location of icecream's gcc and g++.
			  Defaults to /opt/icecream/bin.],
,)

AC_ARG_ENABLE(gtk,
[
  --disable-gtk           Disables gtk+ native widgets.],
,)

AC_ARG_ENABLE(kde,
[
  --disable-kde           Disables KDE native widgets.],
,)

AC_ARG_ENABLE(strip,
[
  --disable-strip
			  Disables the stripping of installed binaries, so 
			  debugging symbols are not removed.],
,)

AC_ARG_ENABLE(post-install-scripts,
[
  --disable-post-install-scripts
			  Disables post install scripts within make install.
			  It is usefull when creating a package from the
			  installed files. The scripts are then run by
			  the package after it is installed.],
,)

AC_ARG_ENABLE(openxml,
[
  --enable-openxml
			  Enable support for open XML file formats, such as
			  bash completion, MIME type detection],
,)

AC_ARG_WITH(arch,
[
  --with-arch             Define the architecture that we will target.

                          Usage: --with-arch=(x86|ppc|sparc)],
,)

AC_ARG_WITH(openclipart,
[
  --with-openclipart      Build and install OOo galleries from the Open Clip
			  Art Library. The parameter is either a version of
			  the library or an absolute path to the prebuilt and
			  preinstalled .png files.
		      
			  Example: --with-openclipart=0.13
				   --with-openclipart=/usr/share/openclipart],
,)

AC_ARG_WITH(dejavu-fonts,
[
  --with-dejavu-fonts     Download and install DejaVu Fonts. The parameter is
                          a version number.
		      
			  Example: --with-dejavu-fonts=2.1],
,)

dnl
dnl Items here only to make --help work nicely:
dnl 
AC_ARG_ENABLE(java,
[
  --with-java             build ooo with a Jdk & Java support],
,)
dnl
dnl Mirror
dnl
AC_ARG_WITH(mirror,
[
  --with-mirror           Specify a mirror of the go-ooo.org packages
			  directory.
			
			  Example: --with-mirror=http://foo.bar.baz/mirror

			  If you want to setup your own mirror, just add
			  something like the following to your cron:

			  rsync -azq --delete --partial go-oo.org::packages \\
				/some/directory/where/you/want/],
,)

AM_MAINTAINER_MODE

dnl
dnl Setup the misc. tweaks we need.
dnl
BASEDIR=`pwd`
TOOLSDIR=$BASEDIR

AC_PATH_PROG(PERL, perl)
if test -z "PERL"; then
   AC_MSG_ERROR([perl not found; required for ooo-build])
fi
AC_SUBST(PERL)
INTLTOOL_PERL=$PERL
AC_SUBST(INTLTOOL_PERL)

AC_PATH_PROG(PKG_CONFIG, pkg-config)
if test ! -x "$PKG_CONFIG" ; then
   AC_MSG_ERROR([
*** You need the latest pkg-config.
*** Get the latest version of pkg-config from
*** <http://www.freedesktop.org/software/pkgconfig/>.])
fi
AC_SUBST(PKG_CONFIG)

PKG_CHECK_MODULES( FOO_COMMON, 
		   [ libxml-2.0 fontconfig >= 1.0.1 ],
		   have_common=true, have_common=false )

# NB. according to:
#   http://sources.redhat.com/ml/bug-automake/2002/msg02066.html
# it's a really bad idea to do conditional tests for things.
#
if test "z$with_win32" = "z" -a "z`uname -o`" = "zCygwin" ; then
    AC_MSG_WARN([On Cygwin, automatically using --with-win32 and --enable-binfilter])
    with_win32="yes"
    enable_binfilter="yes"
    PROPAGATED_ARGS="$PROPAGATED_ARGS --enable-binfilter=yes"
fi
if test "z$with_win32" = "z"; then
    if test ! $have_common; then
	AC_MSG_ERROR([Library requirements were not met])
    fi
    BUILD_WIN32=
else
    if test "z$with_distro" = "z"; then
	with_distro="Win32"
    fi

    BUILD_WIN32=yes
fi

AC_SUBST(BUILD_WIN32)

AM_CONDITIONAL(BUILD_WIN32, [test "$BUILD_WIN32" = yes])

if test "z$with_tag" = "z"; then
   with_tag=$DEFAULT_TAG
fi

if echo "$with_tag" | grep -E ['^(OOO_|oo[a-z]680-|OO[A-Z]680_)'] >/dev/null 2>/dev/null ; then
   APPLY_DIR="$TOOLSDIR/patches/src680"
else
   APPLY_DIR="$TOOLSDIR/patches/$with_tag"
fi

# search more dirs for the apply file
# the default dir is defined by the selected tag
# the alternatives are derived by removing a part (suffix) of the tag
while test "$APPLY_DIR" != "$TOOLSDIR/patches/" ; do
   test -f $APPLY_DIR/apply && break
   APPLY_DIR=${APPLY_DIR%?}
done
test "$APPLY_DIR" = "$TOOLSDIR/patches/" && APPLY_DIR="$TOOLSDIR/patches"
if test -f $APPLY_DIR/apply; then
   AC_MSG_RESULT([found $with_tag, using $APPLY_DIR/apply])
else
   AC_MSG_ERROR([Can't locate patch set for: $with_tag])
fi
CVSTAG=$with_tag
AC_SUBST(CVSTAG)
AC_SUBST(APPLY_DIR)

AC_MSG_CHECKING( for master workspace name )
MWS=`echo ${CVSTAG%[[-_]]*} | tr [[:lower:]] [[:upper:]]`
AC_MSG_RESULT([$MWS])
AC_SUBST(MWS)

# FIXME: We currently do not support starting build with already unpackaged
# sources, so the following warning is always printed
warn_use_download="	./download
"
AC_MSG_CHECKING( for directory where to download sources )
if test "z$with_srcdir" = "z"; then
    SRCDIR=$BASEDIR/src
elif (echo "${with_srcdir}" | grep -v -q "^/"); then
    AC_MSG_ERROR([--with-srcdir= must be an absolute path]);
else
    SRCDIR=$with_srcdir
fi
AC_MSG_RESULT([$SRCDIR])

BUILDDIR=$BASEDIR/build
if test "z$with_ooo_builddir" = "z"; then
   OOBUILDDIR=$BUILDDIR/$CVSTAG
else
   OOBUILDDIR=$with_ooo_builddir
fi

if test "z$with_win32" != "z" -a "z`uname -o`" = "zCygwin" ; then
    AC_MSG_CHECKING([that we use /cygdrive paths])
    case "$OOBUILDDIR" in
	/cygdrive/*) 
	    ;;
	*)  AC_MSG_ERROR([You must use /cygdrive/... style paths, not Cygwin mounts])
	    ;;
    esac
    AC_MSG_RESULT([yes])
fi

rm -f $BASEDIR/stamp/patch.apply

AC_SUBST(SRCDIR)
AC_SUBST(BUILDDIR)
AC_SUBST(TOOLSDIR)
AC_SUBST(OOBUILDDIR)

if test "z$with_win32" != "z"; then
    AC_PATH_PROG(CABEXTRACT, cabextract)
    if test "!" -x "$CABEXTRACT" ; then
      AC_MSG_ERROR([
*** The cabextract utility must be installed.])
    fi
fi

AC_MSG_CHECKING([for distribution name])
case "$with_distro" in
    ""|"yes"|"no") distros_list=`ls distro-configs/*.conf.in | sort | sed -e "s|distro-configs/\(.*\).conf.in|\t\1|g"`
	AC_MSG_ERROR([A distribution must be selected by the option --with-distro. You can choose one from:
$distros_list])
	;;
    NLD) DISTRO="SUSE"
	;;
    NLD*) distro_version="${with_distro#NLD}"
	distro_version="${distro_version#-}"  # remove optional '-'
	DISTRO="SUSE-$distro_version.1"
	;;
    *) DISTRO="$with_distro"
	;;
esac
AC_MSG_RESULT([$DISTRO])
AC_SUBST(DISTRO)

AC_MSG_CHECKING([for vendor name based on the distribution name])
VENDORNAME=OpenOffice
case $DISTRO in
        Ark*) VENDORNAME="Ark" ;;
        Debian*|Ubuntu*) VENDORNAME="Debian" ;;
        Dropline*) VENDORNAME="DroplineGNOME" ;;
        Frugalware*) VENDORNAME="Frugalware" ;;
        Gentoo*) VENDORNAME="Gentoo" ;;
        Mandriva*) VENDORNAME="Mandriva" ;;
        rPath*|rpl*) VENDORNAME="rPath" ;;
        SUSE*|Novell*|NLD*) VENDORNAME="Novell" ;;
        PLD*) VENDORNAME="PLD" ;;
        *) AC_MSG_WARN([Cannot guess vendor from distro - please add it to configure.in]) ;;
esac
AC_MSG_RESULT([$VENDORNAME])
AC_SUBST(VENDORNAME)

if test "z$with_java" = "zno"; then
   AC_PATH_PROG(XSLTPROC, xsltproc, no)
   if test "$XSLTPROC" = "no"; then
      AC_MSG_ERROR([xsltproc is required to build without java])
   fi
fi

AC_CHECK_HEADER(security/pam_appl.h, have_pam=true, have_pam=false)
AC_CHECK_HEADER(png.h, have_png=true, have_png=false)
AC_CHECK_HEADER(zlib.h, have_zlib=true, have_zlib=false)

if test "z$with_win32" = "z"; then
    if test "z$enable_pam" != "zno" && ! $have_pam; then
	AC_MSG_ERROR(install pam-devel)
    fi
    if ! $have_png; then
	AC_MSG_ERROR(install png-devel)
    fi

    if ! $have_zlib; then
	AC_MSG_ERROR(install zlib-devel)
    fi

fi

AC_CHECK_PROG(ACONF, autoconf, yes)
if ! test "z$ACONF" = "zyes"; then
    AC_MSG_ERROR( autoconf must be installed )
fi

AC_CHECK_PROG(FLEX, flex, yes)
if ! test "z$FLEX" = "zyes"; then
   AC_MSG_ERROR( flex must be installed )
fi

AC_CHECK_PROG(BISON, bison, yes)
if ! test "z$BISON" = "zyes"; then
   AC_MSG_ERROR( bison must be installed )
fi

BUILD_NCPUS=1
if test "z$with_num_cpus" != "z"; then
   BUILD_NCPUS=$with_num_cpus
fi
AC_SUBST(BUILD_NCPUS)

AC_CHECK_PROG(CCACHE, ccache, yes)

ENABLE_CCACHE=""
ENABLE_ICECREAM=""
for enable_speedup in `echo "$with_gcc_speedup" | sed 's/,/ /g'`
do
    if test "$enable_speedup" = "ccache"; then
        if test "$CCACHE" != "yes"; then
            AC_MSG_ERROR([ccache not found, but set in --with-gcc-speedup])
        else
            ENABLE_CCACHE="yes"
            if test "z$with_ooo_builddir" = "z"; then
                OOBUILDDIR=$BUILDDIR/current
            fi
            if test -d "$OOBUILDDIR" ; then
                if test -f "$OOBUILDDIR/.tag" ; then
                    save_tag=`cat "$OOBUILDDIR/.tag"`
                    if test "$save_tag" != "$CVSTAG" ; then
                        while test -d "$BUILDDIR/$save_tag" ; do
                            save_tag="$save_tag."
                        done
                        mv "$OOBUILDDIR" "$BUILDDIR/$save_tag"
                    fi
                else
                    AC_MSG_ERROR([Building with ccache, but unable to backup the previous version.
Please move $OOBUILDDIR away...])
                fi
            fi
        fi
    elif test "$enable_speedup" = "icecream"; then
        ENABLE_ICECREAM="yes"
    fi
done
AC_SUBST(ENABLE_CCACHE)
AC_SUBST(ENABLE_ICECREAM)

ICECREAM_JOBS="10"
if test "z$with_icecream_max_jobs" != "z"; then
    if test "$with_icecream_max_jobs" -gt "10"; then
        AC_MSG_WARN([dmake is limited to 10 jobs, using --with-icecream-max-jobs=10 (instead of $with_num_jobs).])
        with_icecream_max_jobs="10"
    fi
    ICECREAM_JOBS="$with_icecream_max_jobs"
fi
AC_SUBST(ICECREAM_JOBS)

ICECREAM_BINDIR="/opt/icecream/bin"
if test "z$with_icecream_bindir" != "z"; then
    ICECREAM_BINDIR="$with_icecream_bindir"
fi
if test "z$ENABLE_ICECREAM" = "zyes"; then
    if test ! -x "$ICECREAM_BINDIR/gcc" -o ! -x "$ICECREAM_BINDIR/g++" ; then
        AC_MSG_ERROR([icecream's gcc and g++ not found, please set --with-icecream-bindir])
    fi
fi
AC_SUBST(ICECREAM_BINDIR)

if test "z$with_distro" = "zKDE"; then
   if test "z$enable_kde" = "z" ; then
        enable_kde=true
   fi
fi

AC_MSG_CHECKING([for widget sets])
OOO_WIDGET_FLAGS=
if test "$enable_kde" != "no"; then
   if test "z$with_win32" = "z"; then
      OOO_WIDGET_FLAGS="--enable-kde "
      widget_sets="kde"
   fi
fi
if test "$enable_gtk" != "no"; then
   if test "z$with_win32" = "z"; then
      OOO_WIDGET_FLAGS="--enable-gtk $OOO_WIDGET_FLAGS"
      widget_sets="gtk $widget_sets"

      PKG_CHECK_MODULES( FOO_GTK, [ gtk+-2.0 ],
         have_gtk=true, have_gtk=false )
      if ! $have_gtk; then
         AC_MSG_ERROR([Gtk+ library requirements were not met])
      fi
   fi
else
   OOO_WIDGET_FLAGS="--disable-gtk $OOO_WIDGET_FLAGS"
fi
OOO_CUSTOM_IMAGES=
OOO_TANGO_IMAGES=ooo_tango_images-1.tar.bz2
OOO_EXTRA_ARTWORK=extras-2.tar.bz2
AC_SUBST(OOO_CUSTOM_IMAGES)
AC_SUBST(OOO_TANGO_IMAGES)
AC_SUBST(OOO_EXTRA_ARTWORK)

AC_MSG_RESULT($OOO_WIDGET_FLAGS)
AC_SUBST(OOO_WIDGET_FLAGS)

if test "z$BUILD_WIN32" = "zyes" -a "z$enable_cairo" = "z" ; then
    AC_MSG_WARN([On Cygwin, automatically using --disable-cairo])
    enable_cairo="no"
fi
if test "$enable_cairo" != "no"; then
   if test "$with_system_cairo" = "yes"; then
	SYSTEM_CAIRO=TRUE
   fi
   CAIRO_ENABLED=TRUE
   if test "z$BUILD_WIN32" = "zyes"; then
      # Use the latest version
      CAIRO_VER=1.2.4 
   else
      # Not really, as Linux builds always use system cairo now
      CAIRO_VER=1.0.2 
      GLITZ_VER=0.4.3
   fi
   CAIRO_CONFIGURE_FLAG="--enable-cairo=yes --with-system-xrender-headers"
   cairo_enabled=yes
else
   CAIRO_ENABLED=
   GLITZ_VER=
   CAIRO_VER=
   CAIRO_CONFIGURE_FLAG="--enable-cairo=no"
   cairo_enabled=no
fi
AC_SUBST(CAIRO_ENABLED)
AC_SUBST(SYSTEM_CAIRO)
AC_SUBST(GLITZ_VER)
AC_SUBST(CAIRO_VER)
AC_SUBST(CAIRO_CONFIGURE_FLAG)

AC_MSG_CHECKING([whether Hunspell UNO is enabled])
if test "$enable_hunspell" == "yes" ; then
   HUNSPELL_UNO_SRC=hunspell_UNO_1.1.tar.gz
   hunspell_uno_enabled="yes"
else
   HUNSPELL_UNO_SRC=
   hunspell_uno_enabled="no"
fi
AC_MSG_RESULT([$hunspell_uno_enabled])
AC_SUBST(HUNSPELL_UNO_SRC)

AC_MSG_CHECKING([for libhunspell])
if test "z$HUNSPELL_UNO_SRC" != "z" ; then
    if test "z$with_system_hunspell" != "zyes" ; then
	HUNSPELL_LIB_SRC=hunspell-1.0.8.tar.gz
	hunspell_lib_result="yes"
    else
	HUNSPELL_LIB_SRC=
	hunspell_lib_result="from system"
    fi
else
	HUNSPELL_LIB_SRC=
	hunspell_lib_result="no needed"
fi
AC_MSG_RESULT([$hunspell_lib_result])
AC_SUBST(HUNSPELL_LIB_SRC)

BUILD_MOZILLA_SOURCE='mozilla-source-1.7.5.tar.gz'
AC_SUBST(BUILD_MOZILLA_SOURCE)

AGFA_MONOTYPE_FONTS_SOURCE='agfa-monotype-fonts.tar.gz'
AC_SUBST(AGFA_MONOTYPE_FONTS_SOURCE)

RUN_POST_INSTALL_SCRIPTS="yes"
if test "z$enable_post_install_scripts" = "zno"; then
   RUN_POST_INSTALL_SCRIPTS="no"
fi
AC_SUBST(RUN_POST_INSTALL_SCRIPTS)

OOO_STRIP="yes"
if test "z$enable_strip" = "zno"; then
   OOO_STRIP="no"
fi
AC_SUBST(OOO_STRIP)

ENABLE_OPENXML="no"
ADD_OPENXML_PRESENTATION_MIME=
ADD_OPENXML_SPREADSHEET_MIME=
ADD_OPENXML_WORD_MIME=
if test "$enable_openxml" = "yes"; then
    ENABLE_OPENXML="yes"
    ADD_OPENXML_PRESENTATION_MIME=\;application/vnd.ms-powerpoint.slideshow.macroEnabled.12\;application/vnd.openxmlformats-officedocument.presentationml.slideshow\;application/vnd.ms-powerpoint.presentation.macroEnabled.12\;application/vnd.openxmlformats-officedocument.presentationml.presentation\;application/vnd.ms-powerpoint.template.macroEnabled.12\;application/vnd.openxmlformats-officedocument.presentationml.template
    ADD_OPENXML_SPREADSHEET_MIME=\;application/vnd.ms-excel.sheet.binary.macroEnabled.12\;application/vnd.ms-excel.sheet.macroEnabled.12\;application/vnd.openxmlformats-officedocument.spreadsheetml.sheet\;application/vnd.ms-excel.template.macroEnabled.12\;application/vnd.openxmlformats-officedocument.spreadsheetml.template
    ADD_OPENXML_WORD_MIME='\;application/vnd.ms-word.document.macroEnabled.12\;application/vnd.openxmlformats-officedocument.wordprocessingml.document\;application/vnd.ms-word.template.macroEnabled.12\;application/vnd.openxmlformats-officedocument.wordprocessingml.template'
fi
AC_SUBST(ENABLE_OPENXML)
AC_SUBST(ADD_OPENXML_PRESENTATION_MIME)
AC_SUBST(ADD_OPENXML_SPREADSHEET_MIME)
AC_SUBST(ADD_OPENXML_WORD_MIME)


ARCHITECTURE="unxlngi4"
if test "z$with_arch" != "z"; then
    if test "z$with_arch" = "zppc"; then
        ARCHITECTURE="unxlngppc"
    fi
    if test "z$with_arch" = "zsparc"; then
        ARCHITECTURE="unxlngs"
    fi
fi
AC_SUBST(ARCHITECTURE)

AC_MSG_CHECKING([for broken aliases])
if `alias gcc > /dev/null 2>&1`; then
   AC_MSG_ERROR( you can't alias your gcc - that's broken );
fi
AC_MSG_RESULT(ok)

AC_MSG_CHECKING([for Open Clip Art Library])
OPENCLIPART_VER=
OPENCLIPART_DIR=
if test "z$with_openclipart" != "z" -a "z$with_openclipart" != "zno" ; then
    if test "z$with_openclipart" = "zyes" ; then
	# the gallery from the source tarball by default
	OPENCLIPART_VER=0.12
    elif (echo "$with_openclipart" | grep "^[[0-9\.]]*$") ; then
	# it's a version number
	OPENCLIPART_VER=$with_openclipart
    elif test "z${with_openclipart%%/*}" = "z" ; then
	# it's an absolute path
	if test -d $with_openclipart ; then
	    OPENCLIPART_DIR=$with_openclipart
	else
	    AC_MSG_ERROR([The directory \"$with_openclipart\" does not exist.])
	fi
    else
	AC_MSG_ERROR([--with-openclipart requires either a version number or an absolute path])
    fi
fi
if test "z$OPENCLIPART_VER" != "z" && ! which inkscape >/dev/null 2>&1 ; then
    AC_MSG_ERROR([inkscape is necessary to process Open Clip Art library from sources])
fi    
AC_MSG_RESULT([${OPENCLIPART_VER:-${OPENCLIPART_DIR:-no}}])
AC_SUBST(OPENCLIPART_VER)
AC_SUBST(OPENCLIPART_DIR)

AC_MSG_CHECKING([for DejaVu Fonts])
DEJAVU_FONTS_VER=
if test "z$with_dejavu_fonts" != "z" -a "z$with_dejavu_fonts" != "zno" ; then
    if test "z$with_dejavu_fonts" = "zyes" ; then
	# the fonts from the source tarball by default
	DEJAVU_FONTS_VER=2.1
    elif (echo "$with_dejavu_fonts" | grep "^[[0-9\.]]*$") ; then
	# it's a version number
	DEJAVU_FONTS_VER=$with_dejavu_fonts
    else
	AC_MSG_ERROR([--with-dejavu-fonts requires either yes, no, or a version number])
    fi
fi
AC_MSG_RESULT([${DEJAVU_FONTS_VER:-no}])
AC_SUBST(DEJAVU_FONTS_VER)

AC_MSG_CHECKING([for ooo-version])
OOO_MAJOR=2.1
OOO_VERSION=2.1
AC_MSG_RESULT([$OOO_VERSION])
AC_SUBST(OOO_VERSION)

AC_MSG_CHECKING([for binsuffix])

if test "z$with_binsuffix" = "zno"; then
    OOO_BINSUFFIX=
elif test "z$with_binsuffix" != "z"; then
    OOO_BINSUFFIX=$with_binsuffix
else
    OOO_BINSUFFIX=2.0
fi
AC_MSG_RESULT([$OOO_BINSUFFIX])
AC_SUBST(OOO_BINSUFFIX)

datestamp=`date '+%Y%m%d.%H%M'`;
SNAPSHOT_TAG="$datestamp.$TAG";
AC_SUBST(SNAPSHOT_TAG)

if test "z$with_installed_ooo_dirname" != "z"; then
   OOOINSTALLDIRNAME=$with_installed_ooo_dirname
else
   OOOINSTALLDIRNAME=ooo-$OOO_MAJOR
fi
AC_SUBST(OOOINSTALLDIRNAME)

if test "z$with_docdir" != "z"; then
    docdir="$with_docdir"
else
    docdir="$datadir/doc/packages/OpenOffice.org"
fi
AC_SUBST(docdir)

# WARNING: The --with-lang= configure option changed, see
#  http://blog.janik.cz/archives/permalinks/2004-11-02T20_04_29.html
#  e.g.
#  --with-lang="ALL"      # see solenv/inc/postset.mk
#  --with-lang="en-US cs"
#  --with-lang="ALL xy"
AC_MSG_CHECKING([for requested localizations])
if test "z$with_lang" = "z" -o "z$with_lang" = "zyes" ; then
    with_lang="en-US"
elif test "z$with_lang" = "zno" ; then
    AC_MSG_WARN([One localization must be built, at least! Defaulting to English.])
    with_lang="en-US"
fi
OOO_LANGS="$with_lang"
AC_MSG_RESULT([$OOO_LANGS])
AC_SUBST(OOO_LANGS)

AC_MSG_CHECKING([for insufficient help localizations])
if test "$with_poor_help_localizations" = "yes" -o \
        "$with_poor_help_localizations" = "no" ; then
    # no default list defined for now, so it is empty for both "yes" and "no"
    OOO_POOR_HELP_LOCALIZATIONS=
else
    OOO_POOR_HELP_LOCALIZATIONS="$with_poor_help_localizations"
fi
AC_MSG_RESULT([$OOO_POOR_HELP_LOCALIZATIONS])
AC_SUBST(OOO_POOR_HELP_LOCALIZATIONS)

AC_MSG_CHECKING([whether to enable Mono bindings])
if test -n "$enable_mono" ; then
    if test "$enable_mono" != "no" ; then
	ENABLE_MONO=TRUE
	mono_enabled=yes
    else
	ENABLE_MONO=FALSE
	mono_enabled=no
    fi
else
    # mono is not explicitly requested. Though, ooo-build will install
    # the extra sources to support the build with mono
    mono_enabled=supported
    ENABLE_MONO=SUPPORTED
fi
AC_MSG_RESULT([$mono_enabled])
AC_SUBST(ENABLE_MONO)

AC_MSG_CHECKING([whether to build the binfilter])
if test "$enable_binfilter" = "yes"; then
    ENABLE_BINFILTER=TRUE
    AC_MSG_RESULT([yes])
else
    ENABLE_BINFILTER=FALSE
    AC_MSG_RESULT([no])
fi
AC_SUBST(ENABLE_BINFILTER)

AC_MSG_CHECKING([whether to build the odk stuff])
if test "$enable_odk" != "no"; then
    ENABLE_ODK=TRUE
    AC_MSG_RESULT([yes])
else
    ENABLE_ODK=FALSE
    AC_MSG_RESULT([no])
fi
AC_SUBST(ENABLE_ODK)

AC_MSG_CHECKING([whether to use prebuilt unowinreg.dll])
if test "$ENABLE_ODK" = "TRUE" -a \
        "$with_win32" != "yes" -a \
        "$with_java" != "no" -a \
        [ -z "$with_mingwin" -o "$with_mingwin" = "no" ] ; then
    USE_PREBUILD_UNOWINREG_DLL=YES
    AC_MSG_RESULT([yes])
else
    USE_PREBUILD_UNOWINREG_DLL=NO
    AC_MSG_RESULT([no])
fi
AC_SUBST(USE_PREBUILD_UNOWINREG_DLL)

AC_MSG_CHECKING([whether to enable graphite support])
# if test -n "$enable_graphite" -o "$enable_graphite" != "no"; then
if test -n "$enable_graphite" -a "$enable_graphite" != "no"; then
   ENABLE_GRAPHITE=TRUE
   graphite_enabled=yes
   GRAPHITE_SRC=silgraphite-1.0.0rc2.tar.gz
else
   ENABLE_GRAPHITE=FALSE
   graphite_enabled=no
fi
AC_MSG_RESULT([$graphite_enabled])
AC_SUBST(ENABLE_GRAPHITE)
AC_SUBST(GRAPHITE_SRC)

AC_MSG_CHECKING([whether to enable Access import])
if test "$enable_access" = "no"; then
    access_enabled=no
    ENABLE_ACCESS=
else
    if test "$with_system_mdbtools" = "yes"; then
        SYSTEM_MDBTOOLS=YES
    else
        MDBTOOLS_SRC=mdbtools-0.6pre1.tar.gz
    fi
    access_enabled=yes
    ENABLE_ACCESS=TRUE
fi
AC_MSG_RESULT([$access_enabled])
AC_SUBST(ENABLE_ACCESS)
AC_SUBST(MDBTOOLS_SRC)
AC_SUBST(SYSTEM_MDBTOOLS)

if test -n "$with_system_libwpd" && test "$with_system_libwpd" = "yes"; then
    SYSTEM_LIBWPD=YES
else
    SYSTEM_LIBWPD=NO
fi
AC_SUBST(SYSTEM_LIBWPD)

if test -n "$with_system_libwps" && test "$with_system_libwps" = "yes"; then
    SYSTEM_LIBWPS=YES
else
    SYSTEM_LIBWPS=NO
fi
AC_SUBST(SYSTEM_LIBWPS)

AC_MSG_CHECKING([whether to enable debugging code])
if test "$enable_dbgutil" = "yes" ; then
    build_product=debug
else
    build_product=product
fi
AC_MSG_RESULT([$build_product])

UPSTREAM_NAME=
UPSTREAM_SOURCE=
UPSTREAM_URI=
if test "$with_tag" = "OOE680_m6" ; then # more milestones to come...
   UPSTREAM_NAME="OOo_2.1.0_src"
   if test "z$with_mirror" != "z" ; then
      UPSTREAM_SOURCE="MIRROR"
      UPSTREAM_URI=
   else
      UPSTREAM_SOURCE="YES"
      UPSTREAM_URI="http://openoffice.bouncer.osuosl.org/?product=OpenOffice.org&os=src_bzip&version=2.1.0"
   fi
fi
AC_SUBST(UPSTREAM_NAME)
AC_SUBST(UPSTREAM_SOURCE)
AC_SUBST(UPSTREAM_URI)

MIRROR="http://go-ooo.org/packages"
if test "z$with_mirror" != "z" ; then
   MIRROR="$with_mirror"
fi
AC_SUBST(MIRROR)

AC_CONFIG_FILES([download], [chmod +x download])
AC_CONFIG_FILES([scratch/place], [chmod +x scratch/place])
AC_CONFIG_FILES([intltool-extract intltool-merge intltool-update], [chmod +x intltool*])
AC_OUTPUT([ 
Makefile
bin/setup
bin/Makefile
desktop/Makefile
desktop/16x16/Makefile
desktop/22x22/Makefile
desktop/24x24/Makefile
desktop/32x32/Makefile
desktop/48x48/Makefile
desktop/scalable/Makefile
distro-configs/Makefile
distro-configs/Ark.conf
distro-configs/Ark64.conf
distro-configs/Common.conf
distro-configs/CommonLinux.conf
distro-configs/CommonWin32.conf
distro-configs/DroplineGNOME.conf
distro-configs/DroplineGNOME64.conf
distro-configs/Debian.conf
distro-configs/DebianEtch.conf
distro-configs/Debian64.conf
distro-configs/DebianEtch64.conf
distro-configs/DebianSarge.conf
distro-configs/Ubuntu.conf
distro-configs/UbuntuL10n.conf
distro-configs/Ubuntu64.conf
distro-configs/UbuntuDapper.conf
distro-configs/UbuntuDapper64.conf
distro-configs/UbuntuEdgy.conf
distro-configs/UbuntuEdgy64.conf
distro-configs/UbuntuFeisty.conf
distro-configs/UbuntuFeisty64.conf
distro-configs/SUSE.conf
distro-configs/SUSE-PPC.conf
distro-configs/SUSE-64.conf
distro-configs/SUSE-9.1.conf
distro-configs/SUSE-9.2.conf
distro-configs/SUSE-9.3.conf
distro-configs/SUSE-10.0.conf
distro-configs/SUSE-10.1.conf
distro-configs/SUSE-10.2.conf
distro-configs/Mandriva.conf
distro-configs/Mandriva64.conf
distro-configs/NovellWin32.conf
distro-configs/NovellWin32ISO.conf
distro-configs/Gentoo.conf
distro-configs/QiLinux.conf
distro-configs/Frugalware.conf
distro-configs/Frugalware64.conf
distro-configs/translate-org-za-linux.conf
distro-configs/translate-org-za-win32.conf
distro-configs/rpl2.conf
distro-configs/rpl2-64.conf
etc/Makefile
patches/Makefile
patches/cairo/Makefile
patches/evo2/Makefile
patches/gstreamer/Makefile
patches/vba/Makefile
patches/mono/Makefile
patches/scsolver/Makefile
patches/src680/Makefile
patches/64bit/Makefile
po/Makefile
doc/Makefile
src/Makefile
src/mono-ooo.pc
stamp/Makefile
templates/Makefile
man/Makefile
www/Makefile
www/images/Makefile
www/tinder-scripts/Makefile
])

if test "$ENABLE_CCACHE" = "yes"; then
    ccache_message="yes"
else
    ccache_message="no"
fi
if test "$ENABLE_ICECREAM" = "yes"; then
    icecream_message="yes, in '$ICECREAM_BINDIR'. Max number of jobs is '$ICECREAM_JOBS'"
else
    icecream_message="no"
fi

echo "
Building openoffice
	tag:                $CVSTAG
	apply rules:        $APPLY_DIR/apply
	build dir:          $OOBUILDDIR
	tools dir:          $TOOLSDIR
	src package dir:    $SRCDIR
	distro:             $DISTRO
	widget sets:        ${widget_sets:-disabled}
	hunspell-uno:	    $hunspell_uno_enabled
	hunspell-lib:	    $hunspell_lib_result
	openclipart:        ${OPENCLIPART_VER:-${OPENCLIPART_DIR:-no}}
	mono bindings:	    $mono_enabled
	mdbtools/access:    $access_enabled
	java:               ${with_java:-yes}
	ooo-install-dir:    $OOOINSTALLDIRNAME
	ccache:             $ccache_message
	icecream:           $icecream_message
	cairo:		    $cairo_enabled
	build type:	    $build_product build
	download mirror:    $MIRROR

To build run:
$warn_use_download	make
	bin/ooinstall <path-to-install>

	This is HEAD - the unstable branch for post-2.0.4 development.
	If you want to build the stable OOo, use:
		ooo-build-2-0-2	 branch for 2.0.2
		ooo-build-2-0-3	 branch for 2.0.3
		ooo-build-2-0-4	 branch for 2.0.4
"
