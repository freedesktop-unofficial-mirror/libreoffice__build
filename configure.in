AC_INIT(bin/build-ooo)
AC_PREREQ(2.51)
AC_PREFIX_DEFAULT(/usr)

OOO_BUILDMAJOR_VERSION=1
OOO_BUILDMINOR_VERSION=9
OOO_BUILDMICRO_VERSION=86
dnl this includes pre-pended period.
OOO_BUILDSTRIKE_VERSION=
OOO_BUILDVERSION="$OOO_BUILDMAJOR_VERSION.$OOO_BUILDMINOR_VERSION.$OOO_BUILDMICRO_VERSION$OOO_BUILDSTRIKE_VERSION"

AM_INIT_AUTOMAKE(ooo-build, $OOO_BUILDVERSION)
AC_SUBST(OOO_BUILDVERSION)

dnl for strike versioning
OOO_BUILD_EXTENSION="(build $OOO_BUILDMICRO_VERSION$OOO_BUILDSTRIKE_VERSION)"
AC_SUBST(OOO_BUILD_EXTENSION)

AC_ARG_WITH(tag,
[
  --with-tag              define the tag to use, defaults to the latest
			  release
			  
			  Example:  --with-tag=OOO_1_0_3],
,)

AC_ARG_WITH(srcdir,
[
  --with-srcdir           define the directory containing the rest of the
			  source code we need to build.],
,)

AC_ARG_WITH(win32,
[
  --with-win32            define the build to be on win32; this sets a number
			  of defaults suitable for that platform.],
,)

AC_ARG_WITH(internal-gcc,
[
  --with-internal-gcc     build our own gcc (gcc-3.4.1 with enum and
			  visibility patches).],
,)

AC_ARG_WITH(distro,
[
  --with-distro           build with a specific distributions patch-set

			  Values are:  NLD (the default), Debian, Ark, ...],
,)

AC_ARG_WITH(lang,
[
  --with-lang             define the localizations to build. The English one
			  is built by default.

			  Examples:  --with-lang="en-US de fr it"
				     --with-lang=ALL],
,)

AC_ARG_ENABLE(devel,
[
  --enable-devel          build the latest bleeding edge untested bleeding
			  edge, un-buildable set of patches.],
,)

AC_ARG_ENABLE(bonobo,
[
  --enable-bonobo         build the ooo bonobo component.],
,)

AC_ARG_WITH(ooo-builddir,
[
  --with-ooo-builddir     define the directory where openoffice.org will be
			  compiled, e.g. the root of an ooo cvs checkout.],
,)

AC_ARG_ENABLE(java,
[
  --enable-java           build ooo with a Jdk & Java support],
,)

AC_ARG_WITH(jdk-home,
[
  --with-jdk-home         specify the path of your JDK.  If you enable Java
			  but do not specify the JDK path, $BUILD_DIR/jdk/bin
			  is assumed. If Java is disabled, this option has
			  no effect.],
,)

AC_ARG_WITH(installed-ooo-dirname,
[
  --with-installed-ooo-dirname
			  specify the directory name of the core OOo network
			  install dir, for example "openoffice" or "ooo-1.1",
			  yielding a core install directory of
			  /usr/lib/openoffice or /usr/lib/ooo-1.1.

			  Example:  --with-installed-ooo-dirname=ooo-1.1],
,)

AC_ARG_WITH(binsuffix,
[
  --with-binsuffix
			  specify the suffix to be used on the end of script
			  names, to allow parallel installation of two versions.
			  
			  Default:  --with-binsuffix=1.9],
,)

AC_ARG_WITH(docdir,
[
  --with-docdir           define the directory name where the extra
			  documentation will be installed.

			  Example:  --with-docdir=/usr/share/doc/packages/OpenOffice.org],
,)

AC_ARG_WITH(vendor,
[
  --with-vendor           specify an overall Vendor name to simplify
			  configuration and packaging when a vendor does the
			  same thing for multiple Distributions],
,)

AC_ARG_WITH(num-cpus,
[
  --with-num-cpus         Number of build processes/cpus to use (number of
			  projects that will build at the same time).
			  Multi-process/multi-cpu builds can save a lot of time
			  on multi-cpu machines.],
,)

AC_ARG_WITH(gcc-speedup,
[
  --with-gcc-speedup      Tool to use to speedup the compilation. Currently
			  ccache (caching results of previous compilation) and
			  icecream (distributed compiling tool) are supported.
			  Both can be used at the same time.

			  Example: --with-gcc-speedup=ccache,icecream],
,)

AC_ARG_WITH(icecream-max-jobs,
[
  --with-icecream-max-jobs
			  Maximum number of jobs that make will issue at
			  the same time. Defaults to 10.],
,)

AC_ARG_WITH(icecream-bindir,
[
  --with-icecream-bindir  Location of icecream's gcc and g++.
			  Defaults to /opt/icecream/bin.],
,)

AC_ARG_ENABLE(gtk,
[
  --disable-gtk           Disables gtk+ native widgets and Gnome icons.],
,)

AC_ARG_ENABLE(kde,
[
  --disable-kde           Disables KDE native widgets and KDE icons.],
,)

AC_ARG_ENABLE(gcc33,
[
  --enable-gcc33          Force use of gcc33/g++33 (ie, compat-gcc) on systems
			  that use gcc 3.4 as the default compiler.],
,)

AC_ARG_ENABLE(post-install-scripts,
[
  --disable-post-install-scripts
			  Disables post install scripts within make install.
			  It is usefull when creating a package from the
			  installed files. The scripts are then run by
			  the package after it is installed.],
,)

AC_ARG_WITH(arch,
[
  --with-arch             Define the architecture that we will target.

                          Usage: --with-arch=(x86|ppc|sparc)],
,)


AM_MAINTAINER_MODE

dnl
dnl Setup the misc. tweaks we need.
dnl
BASEDIR=`pwd`
TOOLSDIR=$BASEDIR

AC_PATH_PROG(PERL, perl)
if test -z "PERL"; then
   AC_MSG_ERROR([perl not found; required for ooo-build])
fi
AC_SUBST(PERL)
INTLTOOL_PERL=$PERL
AC_SUBST(INTLTOOL_PERL)

AC_PATH_PROG(PKG_CONFIG, pkg-config)
if test ! -x "$PKG_CONFIG" ; then
   AC_MSG_ERROR([
*** You need the latest pkg-config.
*** Get the latest version of pkg-config from
*** <http://www.freedesktop.org/software/pkgconfig/>.])
fi
AC_SUBST(PKG_CONFIG)

if test "z$enable_devel" = "z"; then
   latest_flags=
else
   latest_flags=--devel
fi

PKG_CHECK_MODULES( FOO_COMMON, 
		   [ libxml-2.0 fontconfig >= 1.0.1 ],
		   have_common=true, have_common=false )
PKG_CHECK_MODULES( FOO_OPTIONAL,
		   [ gtk+-2.0 libstartup-notification-1.0 ],
		   have_optional=true, have_optional=false )

# NB. according to:
#   http://sources.redhat.com/ml/bug-automake/2002/msg02066.html
# it's a really bad idea to do conditional tests for things.
#
if test "z$with_win32" = "z"; then
    if test ! $have_common; then
	AC_MSG_ERROR([Library requirements were not met])
    fi

    if test "z$enable_bonobo" = "zyes"; then
	ENABLE_BONOBO="yes"
    else
	enable_bonobo="no"
	ENABLE_BONOBO=""
    fi
    BUILD_WIN32=
else
    if test "z$with_distro" = "z"; then
	with_distro="Win32"
    fi

    enable_bonobo="no"
    ENABLE_BONOBO=""

    BUILD_WIN32=yes
fi

AC_SUBST(ENABLE_BONOBO)
AC_SUBST(BUILD_WIN32)
AM_CONDITIONAL(BONOBO, test "z$ENABLE_BONOBO" = "zyes")

AC_MSG_CHECKING( for tag )
if test "z$with_tag" = "z"; then
   with_tag=`$srcdir/bin/latest-patchset $srcdir/patches $latest_flags`;	
   if test "z$with_tag" = "z"; then
      AC_MSG_ERROR( Can't locate latest patchset )
   fi
fi

APPLY_DIR="$TOOLSDIR/patches/$with_tag"
# Exceptions for some tags...

if test -f $APPLY_DIR/apply; then
   AC_MSG_RESULT( found $with_tag, using $APPLY_DIR/apply )
else
   AC_MSG_ERROR( Can't locate patch set for: $with_tag )
fi
CVSTAG=$with_tag
AC_SUBST(CVSTAG)
AC_SUBST(APPLY_DIR)

warn_use_download=
AC_MSG_CHECKING( for tag's source )
if test "z$with_src" = "z"; then
   ext_srcdir="$srcdir/src"
   warn_use_download="	./download
"
   AC_MSG_RESULT( not found )
elif (echo "${with_src}" | grep -v -q "^/"); then
   AC_MSG_ERROR( --with-src= must be an absolute path );
else
   ext_srcdir=$with_src
   if test -d "$with_src/$CVSTAG"; then
      AC_MSG_RESULT( found '$with_src/$CVSTAG' )
   else
      AC_MSG_WARN([
     Missing pristine unpacked source in $with_src/$CVSTAG
     ]);
   fi
fi

WITH_SRC=$with_src
AC_SUBST(WITH_SRC)

if test "z$WITH_SRC" != "z"; then
    BASEDIR=$WITH_SRC
    SRCDIR='"<No manual unpacking required>"'
    BUILDDIR=$BASEDIR
else
    BUILDDIR=$BASEDIR/build
    SRCDIR=$BASEDIR/src
fi

if test "z$with_ooo_builddir" = "z"; then
   OOBUILDDIR=$BUILDDIR/$CVSTAG
else
   OOBUILDDIR=$with_ooo_builddir
fi

AC_SUBST(SRCDIR)
AC_SUBST(BUILDDIR)
AC_SUBST(TOOLSDIR)
AC_SUBST(OOBUILDDIR)

LIBART_VERSION=2.3.13
# RH9 (Shrike) and RHEL3 (Taroon) have older libart which we compensate for
if test "z$with_distro" = "zRHTaroon" -o "z$with_distro" = "zRHShrike"; then
   LIBART_VERSION=2.3.11
fi

PKG_CHECK_MODULES( BAA, libart-2.0 >= $LIBART_VERSION,
		   have_baa=true, have_baa=false )

if test "z$with_win32" = "z"; then
    if test ! $have_baa; then
	AC_MSG_ERROR([You need to install libart development headers])
    fi
else
    AC_PATH_PROG(CABEXTRACT, cabextract)
    if test "!" -x "$CABEXTRACT" ; then
      AC_MSG_ERROR([
*** The cabextract utility must be installed.])
    fi
fi

if test "z$with_distro" = "z"; then
   with_distro="NLD";
fi
DISTRO=$with_distro
AC_SUBST(DISTRO)

VENDORNAME=OpenOffice
if test "z$with_vendor" != "z"; then
   VENDORNAME="$with_vendor"
fi
AC_SUBST(VENDORNAME)

dnl
dnl Debian specific hard-coded defaults.
dnl

dnl
dnl Ark Linux specific hard-coded defaults.
dnl
if test "z$with_distro" = "zArk"; then
    if test "z$with_vendor" = "z"; then
        with_vendor="Ark Linux"
    fi
fi

JDK_HOME=
if test "z$enable_java" != "zno"; then
   ENABLE_JAVA=yes
   if test "z$with_jdk_home" != "z"; then
      JDK_HOME=$with_jdk_home
   fi
else
   AC_PATH_PROG(XSLTPROC, xsltproc, no)
   if test "$XSLTPROC" = "no"; then
      AC_MSG_ERROR([xsltproc is required to build without java])
   fi
   ENABLE_JAVA=no
fi
AC_SUBST(ENABLE_JAVA)
AC_SUBST(JDK_HOME)

AC_CHECK_HEADER(security/pam_appl.h, have_pam=true, have_pam=false)
AC_CHECK_HEADER(png.h, have_png=true, have_png=false)
AC_CHECK_HEADER(zlib.h, have_zlib=true, have_zlib=false)

if test "z$with_win32" = "z"; then
    if ! $have_pam; then
	AC_MSG_ERROR(install pam-devel)
    fi
    if ! $have_png; then
	AC_MSG_ERROR(install png-devel)
    fi

    if ! $have_zlib; then
	AC_MSG_ERROR(install zlib-devel)
    fi

    AC_CHECK_PROG(IMG_MAGIC, convert, yes)
    if ! test "z$IMG_MAGIC" = "zyes"; then
       AC_MSG_ERROR( ImageMagick must be installed )
    fi
fi

AC_CHECK_PROG(ACONF, autoconf, yes)
if ! test "z$ACONF" = "zyes"; then
    AC_MSG_ERROR( autoconf must be installed )
fi

AC_CHECK_PROG(FLEX, flex, yes)
if ! test "z$FLEX" = "zyes"; then
   AC_MSG_ERROR( flex must be installed )
fi

AC_CHECK_PROG(BISON, bison, yes)
if ! test "z$BISON" = "zyes"; then
   AC_MSG_ERROR( bison must be installed )
fi

if test "z$with_internal_gcc" = "zyes" ; then
   SYSTEM_GCC=
   gcc_to_use="internal (will be built)"
else
   SYSTEM_GCC=true
   gcc_to_use="from system"
fi
AC_SUBST(SYSTEM_GCC)

BUILD_NCPUS=1
if test "z$with_num_cpus" != "z"; then
   BUILD_NCPUS=$with_num_cpus
fi
AC_SUBST(BUILD_NCPUS)

AC_CHECK_PROG(CCACHE, ccache, yes)

ENABLE_CCACHE=""
ENABLE_ICECREAM=""
for enable_speedup in `echo "$with_gcc_speedup" | sed 's/,/ /g'`
do
    if test "$enable_speedup" = "ccache"; then
        if test "$CCACHE" != "yes"; then
            AC_MSG_ERROR([ccache not found, but set in --with-gcc-speedup])
        else
            ENABLE_CCACHE="yes"
        fi
    elif test "$enable_speedup" = "icecream"; then
        ENABLE_ICECREAM="yes"
    fi
done
AC_SUBST(ENABLE_CCACHE)
AC_SUBST(ENABLE_ICECREAM)

ICECREAM_JOBS="10"
if test "z$with_icecream_max_jobs" != "z"; then
    if test "$with_icecream_max_jobs" -gt "10"; then
        AC_MSG_WARN([dmake is limited to 10 jobs, using --with-icecream-max-jobs=10 (instead of $with_num_jobs).])
        with_icecream_max_jobs="10"
    fi
    ICECREAM_JOBS="$with_icecream_max_jobs"
fi
AC_SUBST(ICECREAM_JOBS)

ICECREAM_BINDIR="/opt/icecream/bin"
if test "z$with_icecream_bindir" != "z"; then
    ICECREAM_BINDIR="$with_icecream_bindir"
fi
if test "z$ENABLE_ICECREAM" = "zyes"; then
    if test ! -x "$ICECREAM_BINDIR/gcc" -o ! -x "$ICECREAM_BINDIR/g++" ; then
        AC_MSG_ERROR([icecream's gcc and g++ not found, please set --with-icecream-bindir])
    fi
fi
AC_SUBST(ICECREAM_BINDIR)

if test "z$with_distro" = "zKDE"; then
   if test "z$enable_kde" = "z" ; then
        enable_kde=true
   fi
fi

AC_MSG_CHECKING([for widget sets])
OOO_WIDGET_FLAGS=
WITH_ICONS=
if test "$enable_kde" != "no"; then
   OOO_WIDGET_FLAGS="--enable-kde "
   widget_sets="kde"
   WITH_ICONS="crystal"
fi
if test "$enable_gtk" != "no"; then
   OOO_WIDGET_FLAGS="--enable-gtk $OOO_WIDGET_FLAGS"
   widget_sets="gtk $widget_sets"
   WITH_ICONS="industrial $WITH_ICONS"

   if test "z$with_win32" = "z"; then
      PKG_CHECK_MODULES( FOO_GTK, [ gtk+-2.0 ],
         have_gtk=true, have_gtk=false )
      if ! $have_gtk; then
         AC_MSG_ERROR([Gtk+ library requirements were not met])
      fi
   fi
else
   OOO_WIDGET_FLAGS="--disable-gtk $OOO_WIDGET_FLAGS"
fi
OOO_CUSTOM_IMAGES=ooo_custom_images-13.tar.bz2
OOO_EXTRA_ARTWORK=extras-2.tar.bz2
OOO_CRYSTAL_IMAGES=ooo_crystal_images-5.tar.bz2
AC_SUBST(OOO_CUSTOM_IMAGES)
AC_SUBST(OOO_EXTRA_ARTWORK)
AC_SUBST(OOO_CRYSTAL_IMAGES)

AC_MSG_RESULT($OOO_WIDGET_FLAGS)
AC_SUBST(OOO_WIDGET_FLAGS)
AC_SUBST(WITH_ICONS)

FORCE_GCC33=""
if test "z$enable_gcc33" != "z"; then
   FORCE_GCC33="YES"
fi
AC_SUBST(FORCE_GCC33)

RUN_POST_INSTALL_SCRIPTS="yes"
if test "z$enable_post_install_scripts" = "zno"; then
   RUN_POST_INSTALL_SCRIPTS="no"
fi
AC_SUBST(RUN_POST_INSTALL_SCRIPTS)

ARCHITECTURE="unxlngi4"
if test "z$with_arch" != "z"; then
    if test "z$with_arch" = "zppc"; then
        ARCHITECTURE="unxlngppc"
    fi
    if test "z$with_arch" = "zsparc"; then
        ARCHITECTURE="unxlngs"
    fi
fi
AC_SUBST(ARCHITECTURE)

AC_MSG_CHECKING([for broken aliases])
if `alias gcc > /dev/null 2>&1`; then
   AC_MSG_ERROR( you can't alias your gcc - that's broken );
fi
AC_MSG_RESULT(ok)

AC_MSG_CHECKING([for ooo-version])
OOO_MAJOR=1.9
OOO_VERSION=1.9.0
AC_MSG_RESULT([$OOO_VERSION])
AC_SUBST(OOO_VERSION)

AC_MSG_CHECKING([for binsuffix])

if test "z$with_binsuffix" = "zno"; then
    OOO_BINSUFFIX=
elif test "z$with_binsuffix" != "z"; then
    OOO_BINSUFFIX=$with_binsuffix
else
    OOO_BINSUFFIX=1.9
fi
AC_MSG_RESULT([$OOO_BINSUFFIX])
AC_SUBST(OOO_BINSUFFIX)

datestamp=`date '+%Y%m%d.%H%M'`;
SNAPSHOT_TAG="$datestamp.$TAG";
AC_SUBST(SNAPSHOT_TAG)

dnl
dnl Disable bonobo component for now
dnl
dnl if test "z$enable_bonobo" = "zyes"; then
dnl   AC_CONFIG_SUBDIRS(bonobo)
dnl fi

if test "z$with_installed_ooo_dirname" != "z"; then
   OOOINSTALLDIRNAME=$with_installed_ooo_dirname
else
   OOOINSTALLDIRNAME=ooo-$OOO_MAJOR
fi
AC_SUBST(OOOINSTALLDIRNAME)

if test "z$with_docdir" != "z"; then
    docdir="$with_docdir"
else
    docdir="$datadir/doc/packages/OpenOffice.org"
fi
AC_SUBST(docdir)

# WARNING: The --with-lang= configure option changed, see
#  http://blog.janik.cz/archives/permalinks/2004-11-02T20_04_29.html
#  e.g.
#  --with-lang="ALL"      # see solenv/inc/postset.mk
#  --with-lang="en-US cs"
#  --with-lang="ALL xy"
AC_MSG_CHECKING([for requested localizations])
if test "z$with_lang" = "z" -o "z$with_lang" = "zyes" ; then
    with_lang="en-US"
elif test "z$with_lang" = "zno" ; then
    AC_MSG_WARN([One localization must be built, at least! Defaulting to English.])
    with_lang="en-US"
fi
OOO_LANGS="$with_lang"
AC_MSG_RESULT([$OOO_LANGS])
AC_SUBST(OOO_LANGS)

#
# NB. OO.o 2.0 will require checks for
#   + perl-Compress-Zlib
#   + perl-Archive-Zip
#


AC_CONFIG_FILES([download], [chmod +x download])
AC_CONFIG_FILES([intltool-extract intltool-merge intltool-update], [chmod +x intltool*])
AC_OUTPUT([ 
Makefile
ooo1.1.spec
bin/setup
bin/Makefile
desktop/Makefile
distro-configs/Makefile
distro-configs/Ark.conf
distro-configs/Common.conf
distro-configs/Debian.conf
distro-configs/NLD.conf
distro-configs/NLD64.conf
distro-configs/SUSE.conf
fonts/Makefile
patches/Makefile
patches/evo2/Makefile
patches/src680/Makefile
patches/src680-m82/Makefile
patches/src680-m86/Makefile
po/Makefile
doc/Makefile
src/Makefile
stamp/Makefile
man/Makefile
www/Makefile
www/images/Makefile
www/tinder-scripts/Makefile
])

if test "$ENABLE_CCACHE" = "yes"; then
    ccache_message="yes"
else
    ccache_message="no"
fi
if test "$ENABLE_ICECREAM" = "yes"; then
    icecream_message="yes, in '$ICECREAM_BINDIR'. Max number of jobs is '$ICECREAM_JOBS'"
else
    icecream_message="no"
fi

echo "
Building openoffice
	tag:                $CVSTAG
	apply rules:        $APPLY_DIR/apply
	src:                $ext_srcdir
	build dir:          $OOBUILDDIR
	tools dir:          $TOOLSDIR
	src package dir:    $SRCDIR
	distro:             $with_distro
	widget sets:        ${widget_sets:-disabled}
	icons:              ${WITH_ICONS:-default}
	gcc to use:         $gcc_to_use
	ooo-bonobo:         $enable_bonobo
	java:               $ENABLE_JAVA
	ooo-install-dir:    $OOOINSTALLDIRNAME
	force gcc33:        ${FORCE_GCC33:-no}
	ccache:             $ccache_message
	icecream:           $icecream_message

To build run:
$warn_use_download	make
	bin/ooinstall <path-to-install>

	This code is wildly unstable, it will corrupt your children.
	You want the ooo-build-1-3 branch: honest.
"
